plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.11.0"
    id "org.shipkit.shipkit-auto-version" version "1.1.20"
}

group = 'net.gradleutil'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

gradlePlugin {
    plugins {
        gradleJava {
            id = 'net.gradleutil.gradle-java'
            implementationClass = 'net.gradleutil.gradlejava.GradleJavaPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gradleutil/gradle-java'
    vcsUrl = 'https://github.com/gradleutil/gradle-java'
    description = 'Asciidoc gradle util plugin wrapping gradlew with an auto-downloaded java'
    tags = ['build-tool', 'java', 'wrapper', 'gradle-util']

    plugins {
        gradleUtilAsciidoc {
            id = 'net.gradleutil.gradle-java'
            displayName = 'Asciidoc gradle util plugin for wrapping gradlew with an auto-downloaded java'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {}
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}

tasks.withType( Test ){
    group = 'tests'
    testLogging{
        outputs.upToDateWhen{ false }
        showStandardStreams = true
        showCauses = true
        exceptionFormat( 'full' )
    }
}

tasks.withType(AbstractPublishToMaven) { publishTask ->
    def printInfo = { AbstractPublishToMaven task, String repoPath ->
        task.publication.with { p ->
            def sb = new StringBuilder()
            p.artifacts.each {
                String artifactPath = p.groupId.replace(".", "/") + "/" + p.artifactId + "/" + p.version + "/"
                sb.append('\n  ').append(repoPath).append(artifactPath)
                        .append(p.artifactId).append('-').append(p.version)
                        .append(it.classifier ? '-' + it.classifier : '').append('.').append(it.extension)
            }
            logger.lifecycle("Published ${p.groupId}:${p.artifactId}:${p.version}${sb.toString()}")
        }
    }
    doLast {
        if (publishTask instanceof PublishToMavenRepository) {
            printInfo(publishTask, publishTask.repository.url.toString())
        } else {
            String repoPath = repositories.mavenLocal().url.toURL().getFile()
            printInfo(publishTask, repoPath)
        }
    }
}